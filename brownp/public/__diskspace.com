$ ! DISKSPACE.COM
$ ! 8-Sep-87
$ !
$ ! This command file will display disk space statistics for the entire
$ ! system.  Free Blocks and Used Blocks are printed along with percentages
$ ! for each.  To set up DISKSPACE as a DCL command, use the following
$ ! definition:
$ !
$ !			$ DISKS*PACE :== @COM:DISKSPACE.COM
$ !
$ ! In order to run the program, just type DISKSPACE from the DCL prompt.
$ !
$ VERIF=F$VERIFY(0)
$ ON CONTROL_Y THEN GOTO CLEANUP
$ ON WARNING THEN GOTO CLEANUP
$ COUNT=0
$ TOTTOT=0
$ TOTUSED=0
$ TOTFRE=0
$ DATE=F$EXTRACT(0,11,F$TIME())
$ L1=F$LOCATE(".",F$TIME())
$ L2=L1-12
$ TIME=F$EXTRACT(12,L2,F$TIME())
$ DEFINE/USER SYS$OUTPUT SYS$SCRATCH:DISKSPACE.TMP
$ SHOW DEVICE/MOUNTED D
$ WRITE SYS$OUTPUT " VAX/VMS ''F$GETSYI("VERSION")' Disk Usage summary on ''DATE' at ''TIME'"
$ WRITE SYS$OUTPUT ""
$ WRITE SYS$OUTPUT "  Device     Volume Label     Used Blocks      Free Blocks    Total Blocks"
$ WRITE SYS$OUTPUT "----------   ------------    -------------    -------------   ------------"
$ OPEN/READ FILE SYS$SCRATCH:DISKSPACE.TMP
$ READ FILE NULL
$ READ FILE NULL
$ READ_REC:
$ READ/END=DONE FILE REC
$ IF F$LOCATE(":",REC) .EQS. F$LENGTH(REC) THEN GOTO READ_REC
$ DISK=F$EXTRACT(0,F$LOCATE(":",REC),REC)
$ CYL=F$GETDVI("''DISK'","DEVCLASS")
$ IF CYL .NE. 1 THEN GOTO READ_REC
$ FOR=F$GETDVI("''DISK'","FOR")
$ IF FOR THEN GOTO READ_REC
$ LABEL=F$GETDVI("''DISK'","VOLNAM")
$ FREE=F$GETDVI("''DISK'","FREEBLOCKS")
$ TOTAL=F$GETDVI("''DISK'","MAXBLOCK")
$ USED=TOTAL-FREE
$ PERUSE=(USED*100)/TOTAL
$ PERFRE=100-PERUSE
$ LINE=F$FAO("!10AS   !12AS    !7UL (!2UL%)    !7UL (!2UL%)   !12UL", -
              DISK, LABEL, USED, PERUSE, FREE, PERFRE, TOTAL)
$ WRITE SYS$OUTPUT LINE
$ COUNT=COUNT+1
$ TOTTOT=TOTTOT+TOTAL
$ TOTUSED=TOTUSED+USED 
$ TOTFRE=TOTFRE+FREE
$ GOTO READ_REC
$ DONE:
$ CLOSE FILE
$ IF COUNT .LE. 1 THEN GOTO CLEANUP
$ WRITE SYS$OUTPUT ""
$ PERTOTFRE=TOTFRE/(TOTTOT/100)
$ PERTOTUSE=100-PERTOTFRE
$ LINE=F$FAO("Totals on !2UL structures     !8UL (!2UL%)   !8UL (!2UL%)   !12UL", -
COUNT, TOTUSED, PERTOTUSE, TOTFRE, PERTOTFRE, TOTTOT)
$ WRITE SYS$OUTPUT LINE
$ CLEANUP:
$ IF F$TRNLNM("FILE") .NES. "" THEN CLOSE FILE
$ DELETE/NOLOG SYS$SCRATCH:DISKSPACE.TMP;*
$ VERIF=F$VERIFY(VERIF)
$ EXIT
